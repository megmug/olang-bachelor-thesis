/* Can be used as input for https://smlweb.cpsc.ucalgary.ca/start.html to check for some properties, including LL1 */
/* Transformations applied from bnf form:
 * - terminals written small alpha
 * - non-alpha terminals replaced by descriptive alpha names
 * - *, + and ? treated as part of nonterminal names, grammar checker doesnt support them
 * - *, + and ? simulated by new nonterminals
 * - -> instead of ::=
 * - | for every alternative, . at the end of every production
 */

Program                     -> Program' do Command .
Program'                    -> using opensquarebracket
                               ClassDeclaration* ProcedureDeclaration*
                               closesquarebracket
                             | .
ClassDeclaration*           -> ClassDeclaration ClassDeclaration*
                             | .
ClassDeclaration            -> class classname FormalParameterList
                               ClassDeclaration'
                               ClassDeclaration''
                               init Command
                               ClassDeclaration''' .
ClassDeclaration'           -> subclassof classname
                             | .
ClassDeclaration''          -> fields SymbolDeclaration SymbolDeclaration*
                             | .
ClassDeclaration'''         -> opensquarebracket MethodDeclaration MethodDeclaration* closesquarebracket
                             | .
IntSymbolDeclaration        -> int symbolname .
ObjectSymbolDeclaration     -> obj classname symbolname .
SymbolDeclaration           -> IntSymbolDeclaration
                             | ObjectSymbolDeclaration .
FormalParameterList         -> openroundbracket FormalParameterList' closeroundbracket .
FormalParameterList'        -> SymbolDeclaration FormalParameterList''
                             | .
FormalParameterList''       -> comma SymbolDeclaration FormalParameterList''
                             | .
ActualParameterList         -> openroundbracket ActualParameterList' closeroundbracket .
ActualParameterList'        -> Expression ActualParameterList''
                             | .
ActualParameterList''       -> comma Expression ActualParameterList''
                             | .
SymbolDeclaration*           -> SymbolDeclaration SymbolDeclaration*
                             | .
MethodDeclaration           -> method ProcedureHeader Command .
MethodDeclaration*          -> MethodDeclaration MethodDeclaration*
                             | .
ProcedureDeclaration        -> procedure ProcedureHeader Command .
ProcedureHeader             -> symbolname FormalParameterList
                               ProcedureHeader'
                               ProcedureHeader'' .
ProcedureHeader'            -> returns SymbolDeclaration
                             | .
ProcedureHeader''           -> using opensquarebracket ProcedureDeclaration ProcedureDeclaration* closesquarebracket
                             | .
ProcedureDeclaration*       -> ProcedureDeclaration ProcedureDeclaration*
                             | .
Call                        -> SymbolReference Call' .
Call'                       -> ActualParameterList
                             | .
SymbolReference             -> symbolname SymbolReference' .
SymbolReference'            -> dot symbolname
                             | .
Command                     -> SymbolReference assignment Expression
                             | SymbolDeclaration
                             | call Call
                             | read symbolname
                             | opencurlybracket Command Command* closecurlybracket
                             | if Condition then Command
                             | while Condition do Command
                             | printi Expression
                             | prints string
                             | printlns string
                             | error .
Command*                    -> Command Command*
                             | .
Condition                   -> Expression Relation Expression
                             | not Condition .
Relation                    -> equals
                             | smaller
                             | greater .
Expression                  -> Expression' Term Expression'' .
Expression'                 -> plus
                             | minus
                             | .
Expression''                -> Expression''' Term Expression''
                             | .
Expression'''               -> plus
                             | minus .
Term                        -> Factor Term' .
Term'                       -> Term'' Factor Term'
                             | .
Term''                      -> times
                             | divide .
Factor                      -> Call
                             | classname ActualParameterList
                             | integer
                             | openroundbracket Expression closeroundbracket .

/*  symbolname: lowerCamelCase alpha string w/o spaces
 *  integer: sequence of digits
 *  string: any string, enclosed in " "
 *  classname: CamelCase alpha string w/o spaces
 */