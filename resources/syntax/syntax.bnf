/* Can be used as input for "https://bottlecaps.de/rr/ui" to generate syntax graphs */

Program                    ::= ('USING' '[' ClassDeclaration* ProcedureDeclaration* ']')?
                               'DO' Command
ClassDeclaration           ::= 'CLASS' ClassName FormalParameterList
                               ('SUBCLASSOF' ClassName)?
                               ('FIELDS' FieldDeclaration+)?
                               'INIT' Command
                               ('[' MethodDeclaration+ ']')?
ConstDeclaration           ::= 'CONST' SymbolName '=' Number
VarDeclaration             ::= 'VAR' SymbolName
ObjectDeclaration          ::= 'OBJ' ClassName SymbolName
FormalParameterDeclaration ::= VarDeclaration | ObjectDeclaration
FormalParameterList        ::= '(' (FormalParameterDeclaration (',' FormalParameterDeclaration)*)? ')'
ActualParameterList        ::= '(' (Expression (',' Expression)*)? ')'
FieldDeclaration           ::= FormalParameterDeclaration
MethodDeclaration          ::= 'METHOD' ProcedureHeader Command
ProcedureDeclaration       ::= 'PROCEDURE' ProcedureHeader Command
ProcedureHeader            ::= SymbolName FormalParameterList
                               ('RETURNS' FormalParameterDeclaration)?
                               ('USING' '[' ProcedureDeclaration+ ']')?
Call                       ::= SymbolReference ActualParameterList?
SymbolReference            ::= SymbolName ('.' SymbolName)?
Command                    ::= SymbolReference ':=' Expression
                             | ConstDeclaration
                             | VarDeclaration
                             | ObjectDeclaration
                             | 'CALL' Call
                             | 'READ' SymbolName
                             | '{' Command+ '}'
                             | 'IF' Condition 'THEN' Command
                             | 'WHILE' Condition 'DO' Command
                             | 'PRINTI' Expression
                             | 'PRINTS' String
                             | 'PRINTLNS' String
                             | 'ERROR'
Condition                  ::= Expression Relation Expression | 'NOT' Condition
Relation                   ::= '=' | '<' | '>'
Expression                 ::= ('+' | '-')? Term (('+' | '-') Term)*
Term                       ::= Factor (( '*' | '/' ) Factor)*
Factor                     ::= Call
                             | ClassName ActualParameterList  /* Class Instantiation */
                             | Number
                             | '(' Expression ')'

/*  SymbolName: lowerCamelCase alpha string w/o spaces
 *  Number: numeric string w/o spaces
 *  String: any string, enclosed in " "
 *  ClassName: CamelCase alpha string w/o spaces
 */